#!/usr/bin/perl

=head1 NAME

check-snmp-nprocs - Plugin to check processes over snmp.

=head1 SYNOPSIS

    check-snmp-nprocs [ OPTIONS ]

    check-snmp-nprocs --help

=head1 REPORTING BUGS

Please report all bugs to <support(at)bloonix.de>.

=head1 AUTHOR

Jonny Schulz <support(at)bloonix.de>.

=head1 POWERED BY

     _    __ _____ _____ __  __ __ __   __
    | |__|  |     |     |  \|  |__|\  \/  /
    |  . |  |  |  |  |  |      |  | >    <
    |____|__|_____|_____|__|\__|__|/__/\__\

=head1 COPYRIGHT

Copyright (C) 2009-2014 by Jonny Schulz. All rights reserved.

=cut

use strict;
use warnings;
use Bloonix::Plugin;

# --------------------------------------------------
# Plugin options
# --------------------------------------------------

my $plugin = Bloonix::Plugin->new(version => "0.3");

$plugin->example(
    description => "Return a warning state if more than 50 processes running or return a critical state if more than 100 processes are running:",
    arguments => [
        host => "127.0.0.1",
        port => 161,
        community => "public",
        warning => 50,
        critical => 100
    ]
);

$plugin->has_snmp;
$plugin->has_debug;

$plugin->add_option(
    name => "Warning threshold",
    option => "warning",
    value => "count",
    value_type => "number",
    description => "Set the the warning threshold for the maximal count of processes."
);

$plugin->add_option(
    name => "Critical threshold",
    option => "critical",
    value => "count",
    value_type => "number",
    description => "Set the the critical threshold for the maximal count of processes."
);

# --------------------------------------------------
# Parse options
# --------------------------------------------------

my $opt = $plugin->parse_options;
my $oid_procs = "1.3.6.1.2.1.25.4.2.1.2";

# --------------------------------------------------
# SNMP connection
# --------------------------------------------------

my $snmp = $plugin->start_snmp_session;
$snmp->max_msg_size(65535);

# --------------------------------------------------
# Load statistics
# --------------------------------------------------

my $response = $snmp->get_table($oid_procs);

if (!defined $response) {
    my $message = "ERROR: ". $snmp->error;
    $snmp->close;
    $plugin->exit(
        status => "UNKNOWN",
        message => $message
    );
}

$snmp->close;
my $count = scalar keys %$response;

if ($opt->{debug}) {
    foreach my $oid (keys %$response) {
        print STDERR ">> found process $oid -> $response->{$oid}\n";
    }
}

# --------------------------------------------------
# Check warning and critical
# --------------------------------------------------

my $status = "OK";

if ($opt->{critical} && $opt->{critical} <= $count) {
    $status = "CRITICAL";
} elsif ($opt->{warning} && $opt->{warning} <= $count) {
    $status = "WARNING";
}

$plugin->exit(
    status => $status,
    message => "$count processes running",
    stat => { count => $count }
);
