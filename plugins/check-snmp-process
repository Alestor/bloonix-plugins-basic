#!/usr/bin/perl

=head1 NAME

check-snmp-process - Plugin to check a process over snmp.

=head1 SYNOPSIS

    check-snmp-process [ OPTIONS ]

    check-snmp-process --help

=head1 REPORTING BUGS

Please report all bugs to <support(at)bloonix.de>.

=head1 AUTHOR

Jonny Schulz <support(at)bloonix.de>.

=head1 POWERED BY

     _    __ _____ _____ __  __ __ __   __
    | |__|  |     |     |  \|  |__|\  \/  /
    |  . |  |  |  |  |  |      |  | >    <
    |____|__|_____|_____|__|\__|__|/__/\__\

=head1 COPYRIGHT

Copyright (C) 2014 by Jonny Schulz. All rights reserved.

=cut

use strict;
use warnings;
use Bloonix::Plugin;

# --------------------------------------------------
# Plugin options
# --------------------------------------------------

my $plugin = Bloonix::Plugin->new(version => "0.4");

$plugin->example(
    description => "An example to check the number of svchost.exe processes:",
    arguments => [
        host => "127.0.0.1",
        port => 161,
        community => "public",
        warning => "count:lt:5",
        critical => "count:lt:3"
    ]
);

$plugin->add_option(
    name => "Process name",
    option => "process",
    value => "process",
    value_type => "string",
    mandatory => 1,
    description => "This is the process name you want to check."
);

$plugin->has_snmp;
$plugin->has_debug;
$plugin->has_threshold(keys => [ { key => "count" } ]);

$plugin->add_option(
    name => "Match the process name as regex",
    option => "match",
    description => "Use the process name as a regex."
);

# --------------------------------------------------
# Parse options
# --------------------------------------------------

my $opt = $plugin->parse_options;
my $oid_procs   = "1.3.6.1.2.1.25.4.2.1.2";

# --------------------------------------------------
# SNMP connection
# --------------------------------------------------

my $snmp = $plugin->start_snmp_session;
$snmp->max_msg_size(65535);

# --------------------------------------------------
# Load statistics
# --------------------------------------------------

my $response = $snmp->get_table($oid_procs);
my $count = 0;

if (!defined $response) {
    my $message = "ERROR: ". $snmp->error;
    $snmp->close;
    $plugin->exit(
        status => "CRITICAL",
        message => $message
    );
}

foreach my $oid (keys %$response) {
    if ($opt->{debug}) {
        print STDERR ">> found process $oid -> $response->{$oid}\n";
    }

    if (($opt->{match} && $response->{$oid} =~ /$opt->{process}/) || (!$opt->{match} && $response->{$oid} eq $opt->{process})) {
        $count++;
    }
}

$snmp->close;

# --------------------------------------------------
# Check warning and critical
# --------------------------------------------------

my $result = $plugin->check_thresholds(
    stats => { count => $count },
    upshot_keys => [qw(count)]
);

$plugin->exit(
    status => $result->{status},
    message => "$count processes running with name $opt->{process}",
    stats => { count => $count }
);
