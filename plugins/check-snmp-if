#!/usr/bin/perl

=head1 NAME

check-snmp-if - Plugin to check network interfaces by snmp.

=head1 SYNOPSIS

    check-snmp-if [ OPTIONS ]

    check-snmp-if --help

=head1 REPORTING BUGS

Please report all bugs to <support(at)bloonix.de>.

=head1 AUTHOR

Jonny Schulz <support(at)bloonix.de>.

=head1 POWERED BY

     _    __ _____ _____ __  __ __ __   __
    | |__|  |     |     |  \|  |__|\  \/  /
    |  . |  |  |  |  |  |      |  | >    <
    |____|__|_____|_____|__|\__|__|/__/\__\

=head1 COPYRIGHT

Copyright (C) 2014 by Jonny Schulz. All rights reserved.

=cut

use strict;
use warnings;
use Bloonix::Plugin;
use Fcntl qw(:flock :DEFAULT);
use Net::SNMP;

# --------------------------------------------------
# Plugin options
# --------------------------------------------------

my $plugin = Bloonix::Plugin->new(version => "0.3");

$plugin->example(
    description => "Monitor the interface eth0 (1.3.6.1.2.1.31.1.1.1.1) of a linux system:",
    arguments => [
        interface => "eth0",
        host => "127.0.0.1",
        port => 161,
        community => "public",
        "snmp-version" => 1
    ]
);

$plugin->example(
    description => "Monitor the interface 10 (1.3.6.1.2.1.2.2.1.2.10) of a cisco switch:",
    arguments => [
        interface => 10,
        host => "127.0.0.1",
        port => 161,
        community => "public",
        "snmp-version" => 1
    ]
);

$plugin->add_option(
    name => "Network interface",
    option => "interface",
    value => "interface",
    value_type => "string",
    mandatory => 1,
    description => join(" ",
        "This is the network interface you want to check. The interface name should",
        "be the last digit of ifDescr (1.3.6.1.2.1.2.2.1.2.X) or the string that is",
        "found in ifName (1.3.6.1.2.1.31.1.1.1.1)."
    )
);

$plugin->has_snmp;
$plugin->has_debug;

$plugin->has_threshold(
    keys => [
        { key => "in_discards", unit => "bytes" },
        { key => "in_errors", unit => "bytes" },
        { key => "out_discards", unit => "bytes" },
        { key => "out_errors", unit => "bytes" },
        { key => "in_octets", unit => "bytes" },
        { key => "in_ucast_pkts", unit => "bytes" },
        { key => "in_nucast_pkts", unit => "bytes" },
        { key => "in_multicast_pkts", unit => "bytes" },
        { key => "in_broadcast_pkts", unit => "bytes" },
        { key => "out_octets", unit => "bytes" },
        { key => "out_ucast_pkts", unit => "bytes" },
        { key => "out_nucast_pkts", unit => "bytes" },
        { key => "out_multicast_pkts", unit => "bytes" },
        { key => "out_broadcast_pkts", unit => "bytes" }
    ]
);

$plugin->add_option(
    name => "Trigger a CRITICAL if not 'up'",
    option => "crit-if-not-up",
    description => join(" ",
        "This flag enables to check if the status of ifAdminStatus is different from",
        "ifOperStatus. That means that if ifAdminStatus is configured to be up then",
        "ifOperStatus must also been up."
    )
);

$plugin->add_option(
    name => "Debug",
    option => "debug",
    description => "Turn on debugging. Just useful if you want to test something.",
    command_line_only => 1
);

$plugin->add_option(
    name => "No cache",
    option => "no-cache",
    description => "Do not cache SNMP results for 10 seconds.",
    command_line_only => 1
);

# --------------------------------------------------
# Parse options
# --------------------------------------------------

my $opt = $plugin->parse_options;

# --------------------------------------------------
# Some OIDs
# --------------------------------------------------

# Base table oids
my $if_name_base = "1.3.6.1.2.1.31.1.1.1.1";
my $if_base      = "1.3.6.1.2.1.2.2.1";
my $if_hc_base   = "1.3.6.1.2.1.31.1.1.1";

# Some basics, in/out discards and errros
my %if_basics = (
    descr        => "1.3.6.1.2.1.2.2.1.2",
    #speed        => "1.3.6.1.2.1.2.2.1.5",
    admin_status => "1.3.6.1.2.1.2.2.1.7",
    oper_status  => "1.3.6.1.2.1.2.2.1.8",
    in_discards  => "1.3.6.1.2.1.2.2.1.13",
    in_errors    => "1.3.6.1.2.1.2.2.1.14",
    out_discards => "1.3.6.1.2.1.2.2.1.19",
    out_errors   => "1.3.6.1.2.1.2.2.1.20",
);

# 32 bit counters
my %if_counters = (
    in_octets       => "1.3.6.1.2.1.2.2.1.10",
    in_ucast_pkts   => "1.3.6.1.2.1.2.2.1.11",
    in_nucast_pkts  => "1.3.6.1.2.1.2.2.1.12",
    out_octets      => "1.3.6.1.2.1.2.2.1.16",
    out_ucast_pkts  => "1.3.6.1.2.1.2.2.1.17",
    out_nucast_pkts => "1.3.6.1.2.1.2.2.1.18",
);

# High capacity counters
my %if_hc_counters = (
    in_octets          => "1.3.6.1.2.1.31.1.1.1.6",
    in_ucast_pkts      => "1.3.6.1.2.1.31.1.1.1.7",
    in_multicast_pkts  => "1.3.6.1.2.1.31.1.1.1.8",
    in_broadcast_pkts  => "1.3.6.1.2.1.31.1.1.1.9",
    out_octets         => "1.3.6.1.2.1.31.1.1.1.10",
    out_ucast_pkts     => "1.3.6.1.2.1.31.1.1.1.11",
    out_multicast_pkts => "1.3.6.1.2.1.31.1.1.1.12",
    out_broadcast_pkts => "1.3.6.1.2.1.31.1.1.1.13",
);

# ifAdminStatus: 1.3.6.1.2.1.2.2.1.7
my $if_admin_status = {
    1 => "up",
    2 => "down",
    3 => "testing"
};

# ifOperStatus: 1.3.6.1.2.1.2.2.1.8
my $if_oper_status = {
    1 => "up",
    2 => "down",
    3 => "testing",
    4 => "unknown",
    5 => "dormant",
    6 => "notPresent",
    7 => "lowerLayerDown"
};

# --------------------------------------------------
# Get cached or latest SNMP data
# --------------------------------------------------

my ($if_result, $if_hc_result, $if_names);
my ($connection_string, $cache_file, $file_stat, $time, $data);

$connection_string = join("_", "bloonix-check-snmp-if-cache", $opt->{host}, $opt->{community}, $opt->{port});
$connection_string =~ s/\W/_/g;
$cache_file = join("/", $plugin->plugin_libdir, $connection_string);
$cache_file .= ".json";
$file_stat = [ stat($cache_file) ];

#system("cat $cache_file");
sysopen my $fh, $cache_file, O_RDWR | O_CREAT or do {
    $plugin->exit(
        status => "UNKNOWN",
        message => "unable to open file '$cache_file' - $!"
    );
};

flock($fh, LOCK_EX);

if ($file_stat->[7]) {
    my $rest = $file_stat->[7];
    my $offset = 0;
    my $done = 0;

    while ($rest > 0) {
        my $len = sysread($fh, my $buf, $rest, $offset);

        if (!defined $len) {
            next if $! =~ /^Interrupted/;
            $plugin->exit(
                status => "UNKNOWN",
                message => "unable to read from file '$cache_file' - $!"
            );
        }

        $data .= $buf;
        $rest -= $len;
        $offset += $len;
    }
}

if (!$opt->{no_cache} && defined $file_stat->[10] && $file_stat->[10] + 10 >= time && $data && $data =~ /^{.+}$/) {
    $data = JSON->new->decode($data);
    $if_names = $data->{if}->{names};
    $if_result = $data->{if}->{result};
    $if_hc_result = $data->{if}->{hc_result};
    $time = $data->{time};
} else {
    my $snmp = $plugin->start_snmp_session;
    $snmp->max_msg_size(65535);
    $if_result = $snmp->get_table($if_base);
    $if_hc_result = $snmp->get_table($if_hc_base);
    $if_names = $snmp->get_table($if_name_base);
    $time = time;

    sysseek $fh, 0, 0;

    $data = JSON->new->encode({
        time => $time,
        if => {
            names => $if_names,
            result => $if_result,
            hc_result => $if_hc_result
        }
    });

    my $rest = length $data;
    my $offset = 0;

    while ($rest) {
        my $written = syswrite $fh, $data, $rest, $offset;

        if (!defined $written) {
            die "system write error: $!";
        } elsif ($written) {
            $rest -= $written;
            $offset += $written;
        }
    }

    truncate $fh, length $data;
}

close $fh;

if (!defined $if_result) {
    $plugin->exit(
        status => "CRITICAL",
        message => "unable to get oid table from host $opt->{host}\n"
    );
}

# --------------------------------------------------
# Let's have a look how many interfaces exists
# --------------------------------------------------

my $if;
my $search = $if_names // $if_result;

foreach my $oid (keys %$search) {
    if ($oid =~ /(\d+)\z/) {
        my $if_num = $1;

        if ($opt->{debug}) {
            print STDERR ">> found interface $oid -> $search->{$oid}\n";
        }

        if ($opt->{interface} =~ /^\d+\z/) {
            if ($opt->{interface} eq $if_num) {
                $if = $if_num;
            }
        } elsif ($search->{$oid} eq $opt->{interface}) {
            $if = $if_num;
        }

        if (defined $if) {
            if ($opt->{debug}) {
                print STDERR ">> interface $search->{$oid} matched\n";
            }
            last;
        }
    }
}

if (!$if) {
    $plugin->exit(
        status => "UNKNOWN",
        message => "interface $opt->{interface} does not exists"
    );
}

# --------------------------------------------------
# Fetch the statistics from the snmp result
# --------------------------------------------------

my $stat = { };

foreach my $key (keys %if_basics) {
    my $oid = $if_basics{$key};
    if (!exists $if_result->{"$oid.$if"}) {
        $plugin->exit(
            status => "UNKOWN",
            message => "oid $oid.$if does not exists"
        );
    }
    $stat->{$key} = $if_result->{"$oid.$if"};
}

my ($counters, $result);

if ($if_hc_result && exists $if_hc_result->{"1.3.6.1.2.1.31.1.1.1.6"}) {
    $counters = \%if_hc_counters;
    $result = $if_hc_result;
} else {
    $counters = \%if_counters;
    $result = $if_result;
}

foreach my $key (keys %$counters) {
    my $oid = $counters->{$key};
    if (!exists $result->{"$oid.$if"} && $key !~ /nucast/) {
        $plugin->exit(
            status => "UNKOWN",
            message => "oid $oid.$if does not exists"
        );
    }
    $stat->{$key} = $result->{"$oid.$if"} || 0;
}

$stat->{admin_status} = $if_oper_status->{ $stat->{admin_status} };
$stat->{oper_status} = $if_oper_status->{ $stat->{oper_status} };
delete $stat->{descr};

my %delta_keys = (%if_counters, %if_hc_counters);

foreach my $key (keys %delta_keys) {
    if (!exists $stat->{$key}) {
        $stat->{$key} = 0;
    }
}

$plugin->delta(time => $time, stat => $stat, keys => [ keys %delta_keys ]);

# --------------------------------------------------
# Check thresholds
# --------------------------------------------------

if ($opt->{crit_if_not_up}) {
    if ($stat->{admin_status} eq "up" && $stat->{oper_status} ne "up") {
        $plugin->exit(
            status => "CRITICAL",
            message => "ifAdminStatus=$stat->{admin_status}, ifOperStatus=$stat->{oper_status}",
            stats => $stat
        );
    }
}

$plugin->check_thresholds(
    stats => $stat,
    upshot_keys => [qw(admin_status oper_status in_octets out_octets)],
    exit => "yes"
);
